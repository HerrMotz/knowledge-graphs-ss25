PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema1: <http://schema.org/>
PREFIX : <http://ontology.daniel-motz.de/ontology#>

SELECT ?restaurant ?label ?country ?locality ?region ?zip ?address WHERE {
    ?restaurant rdf:type   :Pizzeria.

    # This block is optional, so that incomplete
    # address information or a missing name does 
    # not restrict the query's results.
    OPTIONAL {
        ?restaurant rdfs:label ?label.
        ?restaurant schema1:address ?a.
        OPTIONAL { ?a schema1:addressCountry ?country. }
  		OPTIONAL { ?a schema1:addressLocality ?locality. }
  		OPTIONAL { ?a schema1:addressRegion ?region. }
  		OPTIONAL { ?a schema1:postalCode ?zip. }
  		OPTIONAL { ?a schema1:streetAddress ?address. }
    }

    FILTER NOT EXISTS {
        ?pizza :gehoertZuPizzeria ?restaurant.
        ?pizza :enthaeltZutat ?zutat.
        VALUES ?zutat { :Tomatensauce :tomato }
    }
}